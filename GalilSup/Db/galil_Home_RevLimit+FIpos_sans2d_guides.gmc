NO*****************AXIS ${AXIS}********************
NO***HOME - Reverse Limit, FI pos************
NO***Similar to Home_RevLimit+FIpos but slower and additional waits
NO************JG-ve, FI+ve, PR+ve, FI-ve***************
#HOME${AXIS}
IF (home${AXIS}=1)
    inhome${AXIS}=1
	IF ((home${AXIS}=1) & (hjog${AXIS}=0))
	    slimfl${AXIS}=_FL${AXIS};FL${AXIS}=2147483647
		slimbl${AXIS}=_BL${AXIS};BL${AXIS}=-2147483648
		hjog${AXIS}=1
	ENDIF
	IF ((_LR${AXIS}=0) & (_LF${AXIS}=0) & (home${AXIS}=1))
		home${AXIS}=0;MG "home${AXIS}", home${AXIS};ENDIF
	IF ((hjog${AXIS}=1) & (_BG${AXIS}=1) & (home${AXIS}=1))
		ST${AXIS};ENDIF
	IF ((hjog${AXIS}=1) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_LR${AXIS}<>0))
		olacel${AXIS}=_AC${AXIS};oldecel${AXIS}=_DC${AXIS};speed${AXIS}=_SP${AXIS}
		AC${AXIS}=1024
        DC${AXIS}=1024
		JG${AXIS}=-256
        MG "Jogging to limit", hjog${AXIS}
		WT1000;SH${AXIS};WT1000;BG${AXIS};hjog${AXIS}=2
	ENDIF
	IF ((hjog${AXIS}=1) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_LR${AXIS}=0))
        hjog${AXIS}=2
    ENDIF
	IF ((hjog${AXIS}=2) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_LR${AXIS}=0))
		WT1000
        JG${AXIS}=256
        MG "On limit, finding index", hjog${AXIS}
		WT2000;SH${AXIS};WT1000;
        FI${AXIS};BG${AXIS};hjog${AXIS}=3
	ENDIF
	IF ((hjog${AXIS}=3) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_SC${AXIS}=10))
        WT1000
        SP${AXIS}=128
		PR${AXIS}=400
        MG "PR move", hjog${AXIS}
		WT1500;SH${AXIS};WT1500;BG${AXIS};hjog${AXIS}=4
	ENDIF
	IF ((hjog${AXIS}=4) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_SC${AXIS}=1))
		WT1500
        JG${AXIS}=-20
        MG "Find index", hjog${AXIS}
		WT2000;SH${AXIS};WT1000;FI${AXIS};BG${AXIS};hjog${AXIS}=5
	ENDIF
	IF ((hjog${AXIS}=5) & (_BG${AXIS}=0) & (home${AXIS}=1) & (_SC${AXIS}=10))
        WT1500;MOA;
        SP${AXIS}=speed${AXIS};DC${AXIS}=oldecel${AXIS};AC${AXIS}=olacel${AXIS}
		hjog${AXIS}=0;home${AXIS}=0;homed${AXIS}=1
		MG "home${AXIS}", home${AXIS};MG "homed${AXIS}", homed${AXIS}
	ENDIF
ELSE
	hjog${AXIS}=0
ENDIF
IF ((home${AXIS}=0) & (inhome${AXIS}=1))
    FL${AXIS}=slimfl${AXIS}
	BL${AXIS}=slimbl${AXIS}
	inhome${AXIS}=0
ENDIF
IF (mlock=1)
	II ,,dpon,dvalues
ENDIF
